seed(12345)

####################
# read in the data #
data <- readDiscreteCharacterData("data/discrete.nex")
taxa <- data.taxa()

# global variables
n_taxa <- taxa.size()

# vector for the moves and monitors
moves    = VectorMoves()
monitors = VectorMonitors()

# topology
topology ~ dnUniformTopology(taxa)

moves.append( mvNNI(topology) )
moves.append( mvSPR(topology) )

# branch lengths
n_branches <- 2 * n_taxa - 3
for (i in 1:n_branches) {
   br_lens[i] ~ dnExponential(10.0)
   moves.append( mvScale(br_lens[i]) )
}

# tree length
TL := sum(br_lens)

phylogeny := treeAssembly(topology, br_lens)

# loop over the characters by num state #
max_states <- data.getStateDescriptions().size()
Q := fnJC(max_states)  # simple Mk model
    
# create the substitution model and clamp with observed data
seq ~ dnPhyloCTMC(tree=phylogeny, Q=Q, type="Standard", coding="all")
seq.clamp(data)

mymodel = model(phylogeny)

############
# run MCMC #

# some monitors
monitors.append( mnModel(filename="output/Mk.log", printgen=1) )
monitors.append( mnFile(filename="output/Mk.trees", printgen=1, phylogeny) )

# initialize the MCMC object
mymcmc = mcmc(mymodel, moves, monitors)

# run the MCMC
mymcmc.run(generations=40)

#################
# quit RevBayes #
q()

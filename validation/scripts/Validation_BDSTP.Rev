################################################################################
#
# RevBayes Validation Test
#
# Model: A constant-rate birth-death-sampling-treatment process.
#
#
# authors: Sebastian Hoehna
#
################################################################################

## Global settings


NUM_VALIDATION_REPLICATES = 1000
NUM_MCMC_ITERATIONS       = 10000


#######################
# Reading in the Data #
#######################

# read in the tree which is our observation
tree <- readTrees( "data/primates.tree" )[1]

# Get some useful variables from the data. We need these later on.
taxa <- tree.taxa()

# create the monitor and moves vectors
moves     = VectorMoves()
monitors  = VectorMonitors()



##############
# Tree model #
##############

# Specify a prior on the diversification and turnover rate
diversification ~ dnLognormal(ln(0.01),sd=0.58)
turnover ~ dnLognormal(ln(0.01),sd=0.58)
sampling ~ dnLognormal(ln(0.01),sd=0.58)
treatment ~ dnBeta(2,2)

# now transform the diversification and turnover rates into speciation and extinction rates
speciation := diversification + turnover
extinction := turnover

# specify a prior on the root age (our informed guess is about 75-80 mya)
root <- tree.rootAge()

sampling_fraction <- 1.0

# create some moves that change the stochastic variables
# all moves are sliding proposals but you could use scaling proposals for the rates too
moves.append( mvSlide(diversification,delta=1,tune=true,weight=1) )
moves.append( mvSlide(turnover,delta=1,tune=true,weight=1) )
moves.append( mvSlide(sampling,delta=1,tune=true,weight=1) )
moves.append( mvScale(diversification,lambda=1,tune=true,weight=1) )
moves.append( mvScale(turnover,lambda=1,tune=true,weight=1) )
moves.append( mvScale(sampling,lambda=1,tune=true,weight=1) )
moves.append( mvBetaProbability(treatment,tune=true,weight=2) )

# construct a variable for the tree drawn from a birth death process
psi ~ dnBirthDeathSamplingTreatment(lambda=speciation, mu=extinction, phi=sampling, r=treatment, rootAge=abs(root), Phi=sampling_fraction, taxa=taxa, condition="survival" )

psi.clamp( tree )




#############
# The Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model( root )


mymcmc = mcmc(mymodel, monitors, moves)


validation = validationAnalysis( mymcmc, NUM_VALIDATION_REPLICATES, directory="output_BDSTP" )
validation.burnin(generations=0.1*NUM_MCMC_ITERATIONS,tuningInterval=100)
validation.run(generations=NUM_MCMC_ITERATIONS)
validation.summarize()


# you may want to quit RevBayes now
q()

################################################################################
#
# RevBayes Validation Test: Binomial Distribution
#
# Model: Several random variables from a binomial distribution.
#
#
# authors: Sebastian Hoehna
#
################################################################################


## Global settings

NUM_VALIDATION_REPLICATES = 1000
NUM_MCMC_ITERATIONS       = 10000


# create the monitor and moves vectors
moves     = VectorMoves()
monitors  = VectorMonitors()

n ~ dnUniformNatural(10,20)
p ~ dnBeta(4,2)
moves.append( mvRandomNaturalWalk(n,weight=5) )
moves.append( mvRandomGeometricWalk(n,weight=5) )
moves.append( mvBetaProbability(p, weight=5) )

NUM_VARS = 3
NUM_OBS = 10

for ( j in 1:NUM_VARS ) {

  mean[j] ~ dnBinomial(n,p)
  moves.append( mvRandomNaturalWalk(mean[j],weight=20) )

  for ( i in 1:NUM_OBS){
    index = (j-1)*NUM_OBS + i
	x[index] ~ dnNormal(mean[j], sd=1)
	x[index].clamp( x[index] )
  }
}


#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model(n)





mymcmc = mcmc(mymodel, monitors, moves)

validation = validationAnalysis( mymcmc, NUM_VALIDATION_REPLICATES, directory="output_Binomial" )
validation.burnin(generations=0.1*NUM_MCMC_ITERATIONS,tuningInterval=100)
validation.run(generations=NUM_MCMC_ITERATIONS)
validation.summarize(coverageProbability=0.9)
validation.summarize(coverageProbability=0.5)


# you may want to quit RevBayes now
q()

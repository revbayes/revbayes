################################################################################
#
# RevBayes Validation Test: sampling ultrametric trees with node order constraints.
#
# Model: A constant-rate birth-death process with uniform taxon sampling.
#
#
# authors: Bastien Boussau
#
################################################################################

## Global settings

NUM_VALIDATION_REPLICATES = 10000
NUM_MCMC_ITERATIONS       = 10000


# create the monitor and moves vectors
moves     = VectorMoves()
monitors  = VectorMonitors()

#######################
# Reading in the Data #
#######################

tree <- readTrees("data/abcdefgh.tree")[1]

constraints <- readRelativeNodeAgeConstraints(file="data/NodeOrderConstraints.txt")

# Get some useful variables from the data. We need these later on.
taxa <- tree.taxa()



##############
# Tree model #
##############

# Specify a prior on the diversification and turnover rate
diversification ~ dnGamma(2,2)
turnover ~ dnGamma(2,2)

# now transform the diversification and turnover rates into speciation and extinction rates
speciation := diversification + turnover
extinction := turnover

# specify a prior on the root age (our informed guess is about 75-80 mya)
root <- tree.rootAge()

sampling_fraction <- 23 / 450 # 23 out of the ~ 450 primate species
#sampling_fraction <- 450 / 450 # 450 out of the ~ 450 primate species

# create some moves that change the stochastic variables
# all moves are sliding proposals but you could use scaling proposals for the rates too
moves.append( mvSlide(diversification,delta=1,tune=true,weight=1) )
moves.append( mvSlide(turnover,delta=1,tune=true,weight=1) )

# construct a variable for the tree drawn from a birth death process

psi ~ dnConstrainedNodeOrder(dnBDP(lambda=speciation, mu=extinction, rootAge=abs(root), rho=sampling_fraction, taxa=taxa, condition="nTaxa" ), constraints )

psi.clamp( tree )
psi.setValue( tree )




#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model( root )


mymcmc = mcmc(mymodel, monitors, moves)

validation = validationAnalysis( mymcmc, NUM_VALIDATION_REPLICATES, directory="output_NodeOrderConstraints" )
validation.burnin(generations=0.1*NUM_MCMC_ITERATIONS,tuningInterval=100)
validation.run(generations=NUM_MCMC_ITERATIONS)
validation.summarize()


# you may want to quit RevBayes now
q()

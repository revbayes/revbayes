################################################################################
#
# RevBayes Validation Test: Constant-rate birth-death process
#
# Model: A constant-rate birth-death process with complete taxon sampling.
#
#
# authors: Sebastian Hoehna
#
################################################################################

## Global settings

NUM_VALIDATION_REPLICATES = 1000
NUM_MCMC_ITERATIONS       = 10000


#######################
# Reading in the Data #
#######################

# read in the tree which is our observation
tree <- readTrees( "data/primates.tree" )[1]

# Get some useful variables from the data. We need these later on.
taxa <- tree.taxa()

# create the monitor and moves vectors
moves     = VectorMoves()
monitors  = VectorMonitors()



##############
# Tree model #
##############

# Specify a prior on the diversification and turnover rate
diversification <- 0.0001
turnover <- 0.0199
burst ~ dnBeta(20,2)

# now transform the diversification and turnover rates into speciation and extinction rates
speciation := diversification + turnover
extinction := turnover

# specify a prior on the root age (our informed guess is about 75-80 mya)
root <- tree.rootAge()

sampling_fraction <- 1.0

# create some moves that change the stochastic variables
# all moves are sliding proposals but you could use scaling proposals for the rates too
moves.append( mvBetaProbability(burst,tune=true,weight=2) )

# construct a variable for the tree drawn from a birth death process
psi ~ dnEpisodicFossilizedBirthDeathProcess(lambda=[speciation,speciation], mu=[extinction,extinction], phi=[0.0,0.0], Lambda=[burst], timeline=[abs(root)*0.15], rootAge=abs(root), rho=sampling_fraction, taxa=taxa, condition="survival" )
#psi ~ dnBirthDeathBurstProcess(lambda=speciation, mu=extinction, beta=burst, burstTime=abs(root)*0.15, rootAge=abs(root), rho=sampling_fraction, taxa=taxa, condition="survival" )

psi.clamp( tree )


#############
# The Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model( root )


mymcmc = mcmc(mymodel, monitors, moves)


validation = validationAnalysis( mymcmc, NUM_VALIDATION_REPLICATES, directory="output_Burst" )
validation.burnin(generations=0.1*NUM_MCMC_ITERATIONS,tuningInterval=100)
validation.run(generations=NUM_MCMC_ITERATIONS)
validation.summarize()


# you may want to quit RevBayes now
q()
